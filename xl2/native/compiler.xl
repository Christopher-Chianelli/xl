// ****************************************************************************
//  compiler.xl                     (C) 1992-2003 Christophe de Dinechin (ddd) 
//                                                                 XL2 project 
// ****************************************************************************
// 
//   File Description:
// 
//     The main body of the XL compiler
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This program is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html for details
// ****************************************************************************
// * File       : $RCSFile$
// * Revision   : $Revision$
// * Date       : $Date$
// ****************************************************************************

import SC = XL.SCANNER
import CS = XL.UI.CONSOLE
import PT = XL.PARSER.TREE
import PR = XL.PARSER
import IO = XL.TEXT_IO
import XLT = XL.TRANSLATOR
import CG = XL.CODE_GENERATOR
import SYM = XL.SYMBOLS
import RD = XL.RENDERER
import ASCII = XL.TEXT_IO.ENCODING.ASCII
import ERR = XL.ERRORS

// Things that we import just so that they get compiled
import XL.CONSTANTS
import XL.SEMANTICS.TYPES
import XL.SEMANTICS.DECLARATIONS
import XL.SEMANTICS.TYPES.FUNCTIONS
import XL.SEMANTICS.TYPES.RECORDS
import XL.SEMANTICS.FUNCTIONS
import XL.SEMANTICS.OVERLOAD
import XL.SEMANTICS.WRITTEN_FORMS
import XL.SEMANTICS.INSTRUCTIONS
import XL.SEMANTICS.PRAGMAS
import XL.SEMANTICS.MODULES

// Plugins
import XL.PLUGIN.DIFFERENTIATION
import XL.PLUGIN.CONSTANTFOLD
import XL.PLUGIN.LISTING
import XL.PLUGIN.JUNK
import XL.PLUGIN.TRANSFORMS
import XL.PLUGIN.INCLUDE


procedure Main is
// ----------------------------------------------------------------------------
//   The main entry point
// ----------------------------------------------------------------------------
    I          : integer
    Max        : integer := size CS.arguments
    P          : PR.parser
    theProgram : PT.tree := nil
    compile    : boolean := true
    transcode  : boolean := true

    RD.DefaultRenderer := RD.Open("xl.stylesheet")

    for I in 1..Max-1 loop
        name : text := CS.arguments[i]
        if name = "-show" then
            IO.WriteLn theProgram
        else if name[0] = '+' then
            plugin : text := name[1..length(name)-1]
            compile := false
            SYM.SetText XLT.context, "PHASE", "xlcommand"
            SYM.SetProperty XLT.context, "PRAGMA", PT.NewText(plugin, '"')
            theProgram := SYM.LookupRewrite(XLT.context, plugin, theProgram)
        else if name = "-style" then
            if I + 1 <= Max - 1 then
               I += 1
               RD.Close RD.DefaultRenderer
               RD.DefaultRenderer := RD.Open(CS.arguments[I] + ".stylesheet")
        else if name = "-parse" then
            compile := false
            transcode := false
        else if name = "-sem" then
            compile := false
            theProgram := XLT.ScopeSemantics(theProgram, XLT.scopeMain)
        else if name = "-opt" then
            compile := false
            theProgram := XLT.XLOptimizations(theProgram)
        else if name = "-bootstrap" then
            compile := false
            transcode := true
        else if name = "-ccode" then
            compile := false
            CG.Transcode theProgram, IO.standard_output
        else if name = "-full" then
            compile := true

        else if name = "-g" then
            CG.debug := true

        else
            P := PR.Open(name)
            theProgram := PR.Parse(P)
            PR.Close P
            if compile and ERR.error_count = 0 then
                XLT.Compile theProgram
            else if transcode and ERR.error_count = 0 then
                CG.Transcode theProgram, IO.standard_output

    if ERR.error_count > 0 then
        exit 2
