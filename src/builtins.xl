// Binary operations on integers
x:integer + y:integer           as integer is opcode Add
x:integer - y:integer           as integer is opcode Sub
x:integer * y:integer           as integer is opcode Mul
x:integer / y:integer           as integer is opcode SDiv
x:integer rem y:integer         as integer is opcode SRem
x:integer mod y:integer         as integer is C elfe_mod
x:integer and y:integer         as integer is opcode And
x:integer or y:integer          as integer is opcode Or
x:integer xor y:integer         as integer is opcode Xor
x:integer shl y:integer         as integer is opcode Shl
x:integer ashr y:integer        as integer is opcode AShr
x:integer lshr y:integer        as integer is opcode LShr
-x:integer                      as integer is opcode Neg
not x:integer                   as integer is opcode Not

// Binary operation on real numbers
x:real + y:real                 as real is opcode FAdd
x:real - y:real                 as real is opcode FSub
x:real * y:real                 as real is opcode FMul
x:real / y:real                 as real is opcode FDiv
x:real rem y:real               as real is C fmod
x:real mod y:real               as real is C elfe_modf
-x:real                         as real is opcode FNeg

// Comparison operators
X:integer = Y:integer           as boolean is opcode ICmpEQ
X:integer <> Y:integer          as boolean is opcode ICmpNE
X:integer > Y:integer           as boolean is opcode ICmpSGT
X:integer >= Y:integer          as boolean is opcode ICmpSGE
X:integer < Y:integer           as boolean is opcode ICmpSLT
X:integer <= Y:integer          as boolean is opcode ICmpSLE

X:real = Y:real                 as boolean is opcode FCmpOEQ
X:real <> Y:real                as boolean is opcode FCmpONE
X:real > Y:real                 as boolean is opcode FCmpOGT
X:real >= Y:real                as boolean is opcode FCmpOGE
X:real < Y:real                 as boolean is opcode FCmpOLT
X:real <= Y:real                as boolean is opcode FCmpOLE

X:text = Y:text                 as boolean is C elfe_text_eq
X:text <> Y:text                as boolean is C elfe_text_ne
X:text > Y:text                 as boolean is C elfe_text_gt
X:text >= Y:text                as boolean is C elfe_text_ge
X:text < Y:text                 as boolean is C elfe_text_lt
X:text <= Y:text                as boolean is C elfe_text_le

X:boolean = Y:boolean           as boolean is opcode BCmpEQ
X:boolean <> Y:boolean          as boolean is opcode BCmpNE
X:boolean > Y:boolean           as boolean is opcode BCmpGT
X:boolean >= Y:boolean          as boolean is opcode BCmpGE
X:boolean < Y:boolean           as boolean is opcode BCmpLT
X:boolean <= Y:boolean          as boolean is opcode BCmpLE
X:boolean and Y:boolean         as boolean is opcode BAnd
X:boolean or  Y:boolean         as boolean is opcode BOr
X:boolean xor Y:boolean         as boolean is opcode BXor
not X:boolean                   as boolean is opcode BNot

// Basic math functions
abs X:real                      as real is C fabs
extern real sqrt(real);
extern real sin(real);
extern real cos(real);
extern real tan(real);
extern real asin(real);
extern real acos(real);
extern real atan(real);
extern real exp(real);
extern real expm1(real);
extern real log(real);
extern real log10(real);
extern real log2(real);
extern real log1p(real);
pi is 3.1415926535897932384626433

// If-then-else statement
if true  then TrueBody else FalseBody   is TrueBody
if false then TrueBody else FalseBody   is FalseBody

if true  then TrueBody                  is TrueBody
if false then TrueBody                  is false

// Console / debugging operations
write x:text            as boolean      is C elfe_write_text
write x:integer         as boolean      is C elfe_write_integer
write x:real            as boolean      is C elfe_write_real
write x:character       as boolean      is C elfe_write_character
writeln                 as boolean      is C elfe_write_cr

write true              is write "true"
write false             is write "false"
write head, rest        is write head; write rest
writeln a               is write a; writeln

// Tree shape and attributes
kind  X:tree            as integer      is opcode TreeKind
left  X:prefix          as tree         is opcode TreeLeft
right X:prefix          as tree         is opcode TreeRight
left  X:postfix         as tree         is opcode TreeLeft
right X:postfix         as tree         is opcode TreeRight
left  X:infix           as tree         is opcode TreeLeft
right X:infix           as tree         is opcode TreeRight
name  X:infix           as text         is opcode InfixName
opening X:text          as text         is opcode TextOpening
closing X:text          as text         is opcode TextClosing
child X:block           as tree         is opcode BlockChild
opening X:block         as text         is opcode BlockOpening
closing X:block         as text         is opcode BlockClosing

// Min and max functions
min x, y is if x < min y then x else min y
min y    is y
max x, y is if x > max y then x else max y
max y    is y

// Interval test
X:real in A:real..B:real is X >= A and X <= B

// Factorial function
0! is 1
N:integer! is N * (N-1)!

// Boolean to text conversion
text true  is "true"
text false is "false"

// A few additional text notations
Source:text / From:text /  To:text      is text_replace Source, From, To
Pat:text in Src:text                    is text_index Src, Pat, 0
Pat:text in Src:text from Idx:integer   is text_index Src, Pat, Idx

// A utility function for C-style testing
good false is false
good 0     is false
good 0.0   is false
good ""    is false
good nil   is false
good Other is true

bad X      is not good X

// Loops
while Condition loop Body is
    if Condition then
        Body
        while Condition loop Body
until Condition loop Body               is while not Condition loop Body
loop Body                               is Body; loop Body
for Var in Low..High loop Body is
    Var := Low
    while Var < High loop
        Body
        Var := Var + 1

every Duration, Body is loop { Body; sleep Duration}
Duration:real h  is Duration * 3600
Duration:real m  is Duration * 60
Duration:real s  is Duration
Duration:real ms is Duration * 0.001
